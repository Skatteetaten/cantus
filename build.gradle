buildscript {
  ext.repos = {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven {
      url "http://aurora/nexus/content/groups/public/"
    }
  }
  repositories repos
  dependencies {
    classpath(
        'no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.3.0',
        'org.springframework.boot:spring-boot-gradle-plugin:2.1.1.RELEASE'
    )
  }
}

plugins {
  id 'org.jetbrains.kotlin.plugin.spring' version '1.3.10'
  id 'org.springframework.boot' version '2.1.1.RELEASE'
  id 'org.jlleitschuh.gradle.ktlint' version '6.2.0'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'org.jetbrains.kotlin.jvm' version '1.3.11'
}

repositories repos

ext.aurora = [
    requireStaging   : false,
    applySpockSupport: false
]

apply plugin: 'io.spring.dependency-management'
apply plugin: 'no.skatteetaten.plugins.aurora'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

[jar, distZip]*.enabled = true
[bootJar, distTar, bootDistTar, bootDistZip]*.enabled = false
configurations.archives.artifacts.removeIf { !it.archiveTask.enabled }

group = 'no.skatteetaten.aurora'

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:2.0.2.RELEASE"
  }
}

def junitJupiterVersion = dependencyManagement.importedProperties['junit-jupiter.version']
dependencies {
  compile(
      "com.fasterxml.jackson.module:jackson-module-kotlin",
      "no.skatteetaten.aurora.springboot:aurora-spring-boot2-starter:1.3.1",
      "org.springframework.boot:spring-boot-starter-web",
      "org.springframework.boot:spring-boot-starter-security",
      "org.springframework.boot:spring-boot-starter-actuator",
      "org.springframework.boot:spring-boot-starter-webflux",

      "org.apache.commons:commons-lang3",
      "com.google.guava:guava:27.0.1-jre",
      "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

  )

  [
      'org.springframework.restdocs:spring-restdocs-mockmvc',
      'org.junit.jupiter:junit-jupiter-api',
      "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}",
      'org.springframework.boot:spring-boot-starter-test',
      'io.mockk:mockk:1.8.9',
      'com.willowtreeapps.assertk:assertk:0.10',
      'org.springframework.cloud:spring-cloud-starter-contract-stub-runner',
      'org.springframework.security:spring-security-test',
      'io.projectreactor:reactor-test'
  ].each { testCompile(it) { exclude group: 'junit' } }

  testImplementation(
      "org.junit.jupiter:junit-jupiter-api",
      "com.squareup.okhttp3:mockwebserver:3.10.0"
  )
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

springBoot {
  buildInfo()
}

compileKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
    freeCompilerArgs = ["-Xjsr305=strict"]
  }
}

compileTestKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
  }
}

test {
  useJUnitPlatform()
}
task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
}

ktlint {
  version = '0.29.0'
}